////////////////////////////////////////////////////////////////

/*
A self-resizing generic array for Oak
Jordan Dehmel, 2023
jdehmel@outlook.com
*/

////////////////////////////////////////////////////////////////

package!("std");
use_rule!("std");

include!("std/opt.oak");
include!("std/conv_extra.oak");

////////////////////////////////////////////////////////////////

// A generic self-resizing array.
let stl::vec<t>: struct
{
    data: []t,      // The underlying heap memory
    size: u128,     // The current number of items in the vec
    capacity: u128, // The max amount this can be filled
}
post
{
    Expand<t>(self: ^stl::vec<t>);
    Shrink<t>(self: ^stl::vec<t>);

    Get<t>(self: ^stl::vec<t>, index: i128);
    New<t>(self: ^stl::vec<t>);
    Copy<t>(self: ^stl::vec<t>, other: stl::vec<t>);
    Del<t>(self: ^stl::vec<t>);

    push_back<t>(self: ^stl::vec<t>, what: t);
    append<t>(self: ^stl::vec<t>, what: t);
    pop_back<t>(self: ^stl::vec<t>);
    clear<t>(self: ^stl::vec<t>);
    empty<t>(self: ^stl::vec<t>);

    front<t>(self: ^stl::vec<t>);
    back<t>(self: ^stl::vec<t>);
}

////////////////////////////////////////////////////////////////

// Create an empty vector with capacity 8 by default.
let New<t>(self: ^stl::vec<t>) -> void
{
    self.size = 0;
    self.capacity = 8;

    alloc!(self.data, self.capacity);
}

// Deep copy from another vector
let Copy<t>(self: ^stl::vec<t>, other: stl::vec<t>) -> void
{
    self.size = other.size;

    // Adjust to appropriate size
    while (self.size * 4u128 < self.capacity)
    {
        Shrink(self);
    }

    while (self.size * 2u128 > self.capacity)
    {
        Expand(self);
    }

    // Iteratively copy
    let i: u128 = 0;
    while (i < self.size)
    {
        Copy(ptrarr!(self.data, i), ptrarr!(other.data, i));
        i += 1;
    }
}

// Free the heap memory associated.
let Del<t>(self: ^stl::vec<t>) -> void
{
    if (self.capacity != 0u128)
    {
        free!(self.data);
    }
}

// Get the item of the given index. If the index is out of
// range, it will return `none`.
let Get<t>(self: ^stl::vec<t>, index: i128) -> opt<^t>
{
    let real_index: u128;

    if (index < 0u128)
    {
        real_index = self.size;
        real_index += to_u128(index);
    }
    else
    {
        real_index = to_u128(index);
    }

    let out: opt<^t>;
    wrap_none(@out);

    if (real_index < self.size)
    {
        wrap_some(@out, ptrarr!(self.data, real_index));
    }

    out
}

// Double in size, no matter what
let Expand<t>(self: ^stl::vec<t>) -> void
{
    let new_capacity: u128;
    new_capacity = self.capacity * 2u128;

    // Create aux array
    let new_data: []t;
    alloc!(new_data, new_capacity);

    // Copy into aux array
    let i: u128 = 0;
    while (i < self.size)
    {
        Copy(ptrarr!(new_data, i), ptrarr!(self.data, i));
        i += 1;
    }

    free!(self.data);

    // Set cur array to aux array
    ptrcpy!(self.data, new_data);

    // Set capacity to correct amount
    self.capacity = new_capacity;
}

// Halve in size, so long as capacity >= 16
let Shrink<t>(self: ^stl::vec<t>) -> void
{
    if (self.capacity >= 16u128)
    {
        let new_capacity: u128 = self.capacity / 2u128;

        // Create aux array
        let new_data: []t;
        alloc!(new_data, new_capacity);

        // Copy into aux array
        let i: u128 = 0;
        while (i < self.size && i < self.capacity)
        {
            Copy(ptrarr!(new_data, i), ptrarr!(self.data, i));
            i += 1;
        }

        // Set cur array to aux array
        ptrcpy!(self.data, new_data);

        // Set capacity to correct amount
        self.capacity = new_capacity;
    }
}

// Push an item onto the end of the vector.
let append<t>(self: ^stl::vec<t>, what: t) -> void
{
    push_back(self, what);
}

// Push an item onto the end of the vector.
let push_back<t>(self: ^stl::vec<t>, what: t) -> void
{
    while (self.size * 2u128 > self.capacity)
    {
        Expand(self);
    }

    Copy(ptrarr!(self.data, self.size), what);
    self.size += 1;
}

// Pop the item off the end of the vector.
let pop_back<t>(self: ^stl::vec<t>) -> opt<t>
{
    let out: opt<t>;
    wrap_none(@out);

    if (self.size > 0u128)
    {
        let data: t;
        data = ptrarr!(self.data, self.size - 1);

        self.size -= 1;

        wrap_some(@out, data);

        while (self.size * 4u128 < self.capacity)
        {
            Shrink(self);
        }
    }

    out
}

// Empty the vector
let clear<t>(self: ^stl::vec<t>) -> void
{
    self.size = 0u128;

    while (self.capacity > 8u128)
    {
        Shrink(self);
    }
}

// Return true if the vector is empty.
let empty<t>(self: ^stl::vec<t>) -> bool
{
    let out: bool;

    if (self.size == 0u128)
    {
        out = true;
    }
    else
    {
        out = false;
    }

    out
}

// Return the first item if there is one.
let front<t>(self: ^stl::vec<t>) -> opt<t>
{
    let out: opt<t>;
    out.wrap_none();

    if (self.size > 0)
    {
        out.wrap_some(ptrarr!(self.data, 0));
    }

    return out;
}

// Return the last item if there is one.
let back<t>(self: ^stl::vec<t>) -> opt<t>
{
    let out: opt<t>;
    out.wrap_none();

    if (self.size > 0)
    {
        out.wrap_some(ptrarr!(self.data, self.size - 1));
    }

    return out;
}

////////////////////////////////////////////////////////////////
