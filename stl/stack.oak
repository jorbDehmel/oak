////////////////////////////////////////////////////////////////

/*
A self-resizing generic stl::stack for Oak
Jordan Dehmel, 2023
jdehmel@outlook.com

Header file
*/

////////////////////////////////////////////////////////////////

package!("std");
use_rule!("std");

include!("std/opt.oak");
include!("std/printf.oak");

////////////////////////////////////////////////////////////////

let stl::stack<t>: struct
{
    data: []t,
    size: u128,
    capacity: u128,
}
post
{
    expand<t>(self: ^stl::stack<t>);
    shrink<t>(self: ^stl::stack<t>);

    New<t>(self: ^stl::stack<t>);
    Del<t>(self: ^stl::stack<t>);

    push<t>(self: ^stl::stack<t>, what: t);
    pop<t>(self: ^stl::stack<t>);
    empty<t>(self: ^stl::stack<t>);
    top<t>(self: ^stl::stack<t>);
}

////////////////////////////////////////////////////////////////

let New<t>(self: ^stl::stack<t>) -> void
{
    self.size = 0u128;
    self.capacity = 8u128;

    alloc!(self.data, self.capacity);
}

let Del<t>(self: ^stl::stack<t>) -> void
{
    if (self.capacity != 0u128)
    {
        free!(self.data);
    }
}

// Double in size, no matter what
let expand<t>(self: ^stl::stack<t>) -> void
{
    let new_capacity: u128;
    new_capacity = self.capacity * 2u128;

    // Create aux array
    let new_data: []t;
    alloc!(new_data, new_capacity);

    // Copy into aux array
    let i = 0u128;
    while (i < self.size)
    {
        Copy(ptrarr!(new_data, i), ptrarr!(self.data, i));
        ++i;
    }

    free!(self.data);

    // Set cur array to aux array
    ptrcpy!(self.data, new_data);

    // Set capacity to correct amount
    self.capacity = new_capacity;
}

// Halve in size, so long as capacity >= 16
let shrink<t>(self: ^stl::stack<t>) -> void
{
    if (self.capacity >= 16u128)
    {
        let new_capacity: u128 = self.capacity / 2u128;

        // Create aux array
        let new_data: []t;
        alloc!(new_data, new_capacity);

        // Copy into aux array
        let i = 0u128;
        while (i < self.size && i < self.capacity)
        {
            Copy(ptrarr!(new_data, i), ptrarr!(self.data, i));
            ++i;
        }

        // Set cur array to aux array
        ptrcpy!(self.data, new_data);

        // Set capacity to correct amount
        self.capacity = new_capacity;
    }
}

let push<t>(self: ^stl::stack<t>, what: t) -> void
{
    while (self.size * 2u128 > self.capacity)
    {
        expand(self);
    }

    Copy(ptrarr!(self.data, self.size), what);
    ++self.size;
}

let pop<t>(self: ^stl::stack<t>) -> opt<t>
{
    let out: opt<t>;
    wrap_none(@out);

    if (self.size > 0u128)
    {
        let data: t;
        data = ptrarr!(self.data, self.size - 1u128);

        --self.size;

        wrap_some(@out, data);

        if (self.size > 16u128 && self.size * 4u128
            < self.capacity)
        {
            shrink(self);
        }
    }

    out
}

let empty<t>(self: ^stl::stack<t>) -> bool
{
    if (self.size == 0u128)
    {
        return true;
    }
    return false;
}

let top<t>(self: ^stl::stack<t>) -> opt<t>
{
    let out: opt<t>;
    out.wrap_none();

    if (self.size > 0u128)
    {
        let real_i: u128 = self.size;
        --real_i;
        out.wrap_some(ptrarr!(self.data, real_i));
    }

    return out;
}

////////////////////////////////////////////////////////////////
