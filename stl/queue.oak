////////////////////////////////////////////////////////////////

/*
A generic stl::queue using a singly linked stl::queue for Oak.

Jordan Dehmel, 2023
jdehmel@outlook.com
*/

////////////////////////////////////////////////////////////////

package!("std");
use_rule!("std");

include!("std/opt.oak");
include!("stl/ptrcmp.oak");

////////////////////////////////////////////////////////////////

let stl::queue_node<t>: struct
{
    data: t,
    next: ^stl::queue_node<t>,
}
post
{
    to_void_ptr<stl::queue_node<t>>(_: ^stl::queue_node<t>);
    Del<t>(_: ^stl::queue_node<t>);
}

////////////////////////////////////////////////////////////////

let Del<t>(self: ^stl::queue_node<t>) -> void
{
    if (to_void_ptr(self.next) != to_u128(0))
    {
        Del(self.next);
        free!(self.next);
    }
}

////////////////////////////////////////////////////////////////

let stl::queue<t>: struct
{
    head: ^stl::queue_node<t>,
    tail: ^stl::queue_node<t>,
    size: u128,
}
pre
{
    opt<t>;
    stl::queue_node<t>;
}
post
{
    to_void_ptr<stl::queue_node<t>>(_: ^stl::queue_node<t>);
    Del<t>(_: ^stl::queue<t>);

    front<t>(_: ^stl::queue<t>);

    pop_front<t>(_: ^stl::queue<t>);
    append<t>(_: ^stl::queue<t>, _: t);

    pop<t>(_: ^stl::queue<t>);
    push<t>(_: ^stl::queue<t>, _: t);

    empty<t>(_: ^stl::queue<t>);
}

////////////////////////////////////////////////////////////////

let Del<t>(self: ^stl::queue<t>) -> void
{
    if (to_void_ptr(self.head) != to_u128(0))
    {
        Del(self.head);
        free!(self.head);
    }
}

let front<t>(self: ^stl::queue<t>) -> opt<t>
{
    let out: opt<t>;
    out.wrap_none();

    if (to_void_ptr(self.head) != to_u128(0))
    {
        out.wrap_some(self.head.data);
    }

    return out;
}

let pop_front<t>(self: ^stl::queue<t>) -> void
{
    let old: ^stl::queue_node<t>;
    ptrcpy!(old, self.head);
    ptrcpy!(self.head, self.head.next);

    ptrcpy!(old.next, 0);
    Del(old);
    free!(old);
}

let append<t>(self: ^stl::queue<t>, what: t) -> void
{
    if (to_void_ptr(self.head) == 0)
    {
        alloc!(self.head);
        ptrcpy!(self.tail, self.head);
        self.head.data = what;
    }
    else
    {
        alloc!(self.tail.next);
        ptrcpy!(self.tail, self.tail.next);
        self.tail.data = what;
    }
}

let pop<t>(self: ^stl::queue<t>) -> void
{
    pop_front(self);
}

let push<t>(self: ^stl::queue<t>, what: t) -> void
{
    append(self, what);
}

let empty<t>(self: ^stl::queue<t>) -> bool
{
    if (is_null(to_void_ptr(self.head)))
    {
        return true;
    }
    return false;
}

////////////////////////////////////////////////////////////////
